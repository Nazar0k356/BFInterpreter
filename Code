.model tiny

.data
   endCode dw ?
   buff dw 10000 dup(?)
   code_buffer db 10000 dup(?)

.code
org 100h
   start:
   mov di, offset buff
   mov cx, 10000
   rep stosw

    mov cl, ds:[80h]
    mov bx, 81h
    add bx, cx
    mov byte ptr ds:[bx], 0

    mov ax, 3d00h ; syscall open file
    mov dx, 82h ; address at which command line is stored 
    int 21h
    mov bx, ax 
 
    mov ah, 3fh ; syscall read file
    mov cx, 10000 ; bytes to read
    lea dx, code_buffer ; to read into code_buffer
    int 21h

    mov di, offset code_buffer
    add ax, di
    mov endCode, ax 

   mov bx, offset buff
   dec di
   mainLoop:
   inc di
   cmp di, endCode
   je fin

   mov al, [di] ;Putting symbol of the code into al

   cmp al, 2Bh ;Comparison with +
   jne minus
    mov al, [bx]
    inc al
    mov [bx], al
    jmp mainLoop

   minus:
   cmp al, 2Dh ;Comparison with -
   jne left
    mov al, [bx]
    dec al
    mov [bx], al
    jmp mainLoop

   left:   
   cmp al, 3Ch ;Comparison with <
   jne right
    sub bx, 2
    jmp mainLoop

   right:
   cmp al, 3Eh ;Comparison with >
   jne coma
    add bx, 2
    jmp mainLoop

   coma: 
   cmp al, 2Ch ;Comparison with ,
   jne dot
    mov ah, 3fh
    mov dx, bx
    push bx
    mov bx, 0
    mov cx, 1
    int 21h

    pop bx
    cmp ax, 0
    jne mainLoop

    mov al, 0ffh
    mov [bx], al
    jmp mainLoop

   dot: 
   cmp al, 2Eh ;Comparison with .
   jne lbracket
    mov ah, 02h
    mov dl, [bx]
    ;Checking for enter
    cmp dl, 0dh ;Comparing to cr
    je mainLoop
    int 21h
    jmp mainLoop

   fin:  
   mov ah, 4ch 
   int 21h 

   lbracket:   
   cmp al, 5Bh ;Comparison with [
   jne rbracket 
    mov al, [bx]
    cmp al, 0
    je isZero
    tomain: jmp mainLoop

   rbracket:
   cmp al, 5Dh ;Comparison with ]  
   jne tomain ;This is a comment
    mov al, [bx]
    cmp al, 0
    jne notZero
    jmp mainLoop  
    notZero:
      mov cx, 1
     findBracket1:
     cmp cx, 0
     je tomain
     dec di
     mov al, [di]
     cmp al, 5Bh ;Comparison with [
     jne case1
     dec cx
     jmp findBracket1
     case1:
     cmp al, 5Dh ;Comparison with ] 
     jne findBracket1
     inc cx
     jmp findBracket1

   isZero:
     mov cx, 1
     findBracket2:
     cmp cx, 0
     je tomain
     inc di
     mov al, [di]
     cmp al, 5Bh ;Comparison with [
     jne case2
     inc cx
     jmp findBracket2
     case2:
     cmp al, 5Dh ;Comparison with ] 
     jne findBracket2
     dec cx
     jmp findBracket2  
end start